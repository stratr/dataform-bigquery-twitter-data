config {
  type: "view"
}

js {
  const groupFields = [
    'party',
    'parliament_group'
  ];
}

with previous30 as (
  select
    ${groupFields},
    sum(tweets) as tweets
  from
    ${resolve('tweets_per_day')}
  where
    date >= current_date() -31
  group by
    ${groupFields}
),
preceding30 as (
  select
    ${groupFields},
    sum(tweets) as tweets
  from
    ${resolve('tweets_per_day')}
  where
    date >= current_date() -61
    and date < current_date() -31
  group by
    ${groupFields}
),
joined_data as (
  select
    A.*,
    B.tweets as tweets_previous,
    rank() over(
      order by
        A.tweets desc
    ) as activity_rank,
    rank() over(
      order by
        B.tweets desc
    ) as activity_rank_previous
  from
    previous30 A
    left join preceding30 B using(party)
)
select
  *,
  (
    case
      when (activity_rank_previous - activity_rank) = 0 then '-'
      when (activity_rank_previous - activity_rank) > 0 then '+' || cast(
        (activity_rank_previous - activity_rank) as STRING
      )
      when (activity_rank_previous - activity_rank) < 0 then cast(
        (activity_rank_previous - activity_rank) as STRING
      )
    end
  ) as activity_rank_change,
  tweets - tweets_previous as activity_change
from
  joined_data
order by
  activity_rank
