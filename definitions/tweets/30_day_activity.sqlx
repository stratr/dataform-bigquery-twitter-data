config {
  type: "view"
}

with previous30 as (
  select
    name,
    screen_name,
    party,
    parliament_group,
    sum(tweets) as tweets
  from
    ${resolve('tweets_per_day')}
  where
    date >= current_date() -31
  group by
    1,
    2,
    3,
    4
),
preceding30 as (
  select
    name,
    screen_name,
    party,
    parliament_group,
    sum(tweets) as tweets
  from
    ${resolve('tweets_per_day')}
  where
    date >= current_date() -61
    and date < current_date() -31
  group by
    1,
    2,
    3,
    4
),
joined_data as (
  select
    A.*,
    B.tweets as tweets_previous,
    rank() over(
      order by
        A.tweets desc
    ) as activity_rank,
    rank() over(
      order by
        B.tweets desc
    ) as activity_rank_previous,
    rank() over(
      partition by A.party
      order by
        A.tweets desc
    ) as activity_rank_party
  from
    previous30 A
    left join preceding30 B using(name)
)
select
  *,
  (
    case
      when (activity_rank_previous - activity_rank) = 0 then '-'
      when (activity_rank_previous - activity_rank) > 0 then '+' || cast(
        (activity_rank_previous - activity_rank) as STRING
      )
      when (activity_rank_previous - activity_rank) < 0 then cast(
        (activity_rank_previous - activity_rank) as STRING
      )
    end
  ) as activity_rank_change,
  tweets - tweets_previous as activity_change
from
  joined_data
  order by activity_rank
