config {
  type: "table",
  tags: ["tweets"],
  description: "Data on the user activity for the last 30 days compared to the previous 30 days.",
  bigquery: {
    clusterBy: ["party", "name"]
  }
}

js {
  const groupFields = [
    'name',
    'screen_name',
    'party',
    'parliament_group',
    'datastudio_filter'
  ];
}

with previous30 as (
  select
    ${groupFields},
    sum(tweets) as tweets,
    -- user_followers for the last date of the period
    array_agg(
      user_followers
      order by
        date desc
    ) [safe_offset(0)] as user_followers,
    array_agg(
      active_finnish_parliament
      order by
        date desc
    ) [safe_offset(0)] as active_finnish_parliament
  from
    ${ref('tweets_per_day')}
  where
    date >= current_date() -30
  group by
    ${groupFields}
),
preceding30 as (
  select
    ${groupFields},
    sum(tweets) as tweets,
    -- user_wollowers for the last date of the period
    array_agg(
      user_followers
      order by
        date desc
    ) [safe_offset(0)] as user_followers
  from
    ${ref('tweets_per_day')}
  where
    date >= current_date() -60
    and date < current_date() -30
  group by
    ${groupFields}
),
joined_data as (
  select
    A.*,
    B.user_followers as user_followers_previous,
    rank() over(
      order by
        A.user_followers desc
    ) as user_followers_rank,
    rank() over(
      order by
        B.user_followers desc
    ) as user_followers_rank_previous,
    B.tweets as tweets_previous,
    rank() over(
      order by
        A.tweets desc
    ) as activity_rank,
    rank() over(
      order by
        B.tweets desc
    ) as activity_rank_previous,
    rank() over(
      partition by A.party
      order by
        A.tweets desc
    ) as activity_rank_party
  from
    previous30 A
    left join preceding30 B using(name)
)
select
  current_datetime() as datetime_updated,
  *,
  (
    case
      when (
        user_followers_rank_previous - user_followers_rank
      ) = 0 then '-'
      when (
        user_followers_rank_previous - user_followers_rank
      ) > 0 then '+' || cast(
        (
          user_followers_rank_previous - user_followers_rank
        ) as STRING
      )
      when (
        user_followers_rank_previous - user_followers_rank
      ) < 0 then cast(
        (
          user_followers_rank_previous - user_followers_rank
        ) as STRING
      )
    end
  ) as user_followers_rank_change,
  user_followers - user_followers_previous as user_followers_change,
  (
    case
      when (activity_rank_previous - activity_rank) = 0 then '-'
      when (activity_rank_previous - activity_rank) > 0 then '+' || cast(
        (activity_rank_previous - activity_rank) as STRING
      )
      when (activity_rank_previous - activity_rank) < 0 then cast(
        (activity_rank_previous - activity_rank) as STRING
      )
    end
  ) as activity_rank_change,
  tweets - tweets_previous as activity_change
from
  joined_data
