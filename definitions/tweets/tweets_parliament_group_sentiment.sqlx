config {
  type: "incremental",
  bigquery: {
    partitionBy: "date_trunc(date, MONTH)",
    clusterBy: ["party", "name"]
  },
  tags: ['daily']
}

WITH min_prediction_date as (
  select
    max(max_date_training_data) as min_date
  from
    ${ref("sentiment_train_model")}
),
tweets AS (
  SELECT
    date,
    id_str,
    name,
    party,
    parliament_group,
    ML.NGRAMS(
      array_concat(
        array(
          (
            select
              '@mention'
            from
              unnest(user_mentions) as m
          )
        ),
        array(
          (
            select
              concat('@mp', m.parliament_group)
            from
              unnest(mp_mentions) as m
          )
        ),
        array(
          (
            select
              w
            from
              unnest(words) as w
            where
              not regexp_contains(w, r'^@')
          )
        )
      ),
      [1,
        2]
    ) as ngrams
  FROM
    ${ref('tweets')}
  WHERE
    ${when(incremental(), `date > date_checkpoint and`)} date > (
      select
        min_date
      from
        min_prediction_date
    )
    and parliament_group IS NOT NULL
    AND lang = 'fi'
    and array_length(words) > 0
)
SELECT
  date,
  id_str,
  name,
  party,
  parliament_group,
  (
    select
      prob.prob
    from
      unnest(predicted_review_label_probs) as prob
    where
      prob.label = 'Hallitus'
  ) as tweet_government_prob,
  predicted_review_label,
  predicted_review_label_probs
FROM
  ML.PREDICT(
    MODEL `tanelis.models.predict_parliament_group`,
    (
      SELECT
        *
      FROM
        tweets
    )
  )

pre_operations {
  ${
    when(incremental(), `declare date_checkpoint default (
              select
                max(date)
              from
                ${self()}
            );`)
  }
}
