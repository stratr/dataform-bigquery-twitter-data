config {
  type: "table",
  tags: ["daily"],
  description: "Monthly user followers trend data.",
  bigquery: {
    partitionBy: "month",
    clusterBy: ["party", "name"]
  }
}

with monthly_data as (
  select
    date_trunc(date, MONTH) as month,
    name,
    party,
    parliament_group,
    array_agg(government_role order by date desc)[safe_offset(0)] as government_role,
    array_agg(
      user_followers
      order by
        date desc
    ) [safe_offset(0)] as user_followers_last
    -- always the last value of the month
  from
    ${resolve("tweets_per_day")}
  group by
    1,
    2,
    3,
    4
),
followers_previous as (
  select
    *,
    lag(user_followers_last) over(
      partition by name
      order by
        month asc
    ) as user_followers_previous,
    if(parliament_group = 'Hallitus', user_followers_last, null) as user_followers_last_hallitus,
    if(parliament_group = 'Oppositio', user_followers_last, null) as user_followers_last_oppositio
  from
    monthly_data
)
select
  *,
  coalesce(user_followers_last - user_followers_previous, 0) as user_followers_change
from
  followers_previous
