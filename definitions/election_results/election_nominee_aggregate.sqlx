config {
  type: "table",
  description: "Election results from different elections aggregated on nominee level in one table."
}

js {
  const groupByCols = [
    'year',
    'election',
    'electoral_district_name',
    'name',
    'nominee_election_id',
    'party',
    'party_short',
    'rank_in_electoral_district',
    'comparison_number',
    'profession',
    'gender',
    'elected',
    'home_town',
    'age_election_day'
  ]
}

-- TODO:
-- tee sellainen, että samaan tauluun voi yhdistää muiden vaalien (eri vuodet ja eri vaalit) tulokset myös

with grouped_data as (
  select
    ${groupByCols},
    sum(total_votes) as total_votes
  from
    ${ref('election_nominee_results')}
  group by
    ${groupByCols}
)
select
  *,
  if(
    party is not null,
    rank() over(
      partition by party
      order by
        total_votes desc
    ),
    null
  ) as votes_rank_within_party,
  if(
    party is not null,
    rank() over(
      order by
        total_votes desc
    ),
    null
  ) as votes_rank_within_country
from
  grouped_data
order by
  elected desc,
  total_votes desc
